# collections

#
# ARRAY
#

func new_array size do
	size ++ sizeof(ptr) * malloc let array
	array size !ptr
	array
end

func array_get_size arr do
	arr ?ptr
end

func array_get_index arr index do
	arr array_get_size let size
	index size >= if "array index out ouf bounds" error fail end
	index 0 < if "array index out ouf bounds" error fail end
	arr index ++ sizeof(ptr) * + ?ptr
end

func array_set_index arr index value do
	arr array_get_size let size
	index size >= if "array index out ouf bounds" error fail end
	index 0 < if "array index out ouf bounds" error fail end
	arr index ++ sizeof(ptr) * + value !ptr
end

#
# LIST
#
# [size, ptr]
#

func new_list do
	2 let size 				# default list size
	2 new_array let list			# size of list struct
	size new_array let list_array
	list 0 list_set_size			# set size of list to 0
	list list_array list_set_array		# set pointer to array
	list
end

func list_get_size list do
	list 0 array_get_index
end

func list_set_size list size do
	list 0 size array_set_index
end

func list_get_array list do
	list 1 array_get_index
end

func list_set_array list array do
	list 1 array array_set_index
end

func list_clear list do
	list 0 list_set_size
end

func list_add list value do list value list_push end

func list_push list value do
	# check if capacity is reached
	list list_get_size list list_get_array array_get_size = if
		
		list list_get_array let old_list_array
		old_list_array array_get_size 2 * new_array let new_list_array
		0 let i
		while i old_list_array array_get_size != do
			old_list_array i array_get_index let tmp_value
			new_list_array i tmp_value array_set_index
			i ++ let i
		end		
		list new_list_array list_set_array
	end
	list list_get_array list list_get_size value array_set_index
	list list list_get_size ++ list_set_size
end

func list_get_index list index do
	index list list_get_size >= if "list index out of bounds" error fail end
	index 0 < if "list index out of bounds" error fail end
	list list_get_array index array_get_index
end

func list_set_index list index value do
	index list list_get_size >= if "list index out of bounds" error fail end
	index 0 < if "list index out of bounds" error fail end
	list list_get_array index value array_set_index
end

func list_pop list do
	list list_peek let value
	list list list_get_size -- list_set_size
	value
end

func list_peek list do
	list list_get_size 0 = if "list index out of bounds" error fail end
	list list list_get_size -- list_get_index
end

func list_reverse list do
	0 let tmp_ptr1
	list list_get_size -- let tmp_ptr2
				
	while tmp_ptr2 tmp_ptr1 > do
		list tmp_ptr1 list_get_index let tmp_v1
		list tmp_ptr2 list_get_index let tmp_v2
		list tmp_ptr1 tmp_v2 list_set_index
		list tmp_ptr2 tmp_v1 list_set_index
		tmp_ptr1 ++ let tmp_ptr1
		tmp_ptr2 -- let tmp_ptr2
	end
end

func list_get_index_of_string list string do
	0 let i
	while list list_get_size i != do
		list i list_get_index string streq if
			i ret
		end
		i ++ let i
	end
	-1
end

func list_force_get_index_of_string list string do
	list string list_get_index_of_string -1 = if
		string strlen ++ malloc let tmp_string
		tmp_string string strcpy
		list tmp_string list_add
	end
	list string list_get_index_of_string
end
