# memory


func malloc bytes do
	CHUNK_ALLOCED bytes malloc_type
end


#[size, state, data...]
# bytes -- ptr
func malloc_type type bytes do
	setup_mem
	
	get_start_chunk let current_chunk
	get_start_chunk let first_free_chunk
	
	while true do
		
		current_chunk get_end_chunk >= if
			"malloc out of memory" error fail
		end
		
		current_chunk get_chunk_state let current_chunk_state
		current_chunk get_next_chunk let current_next_chunk
		
		
		current_chunk_state CHUNK_FREE != if
			current_next_chunk let first_free_chunk
			
		else
			bytes CHUNK_OVERHEAD + let required_size1
			bytes CHUNK_OVERHEAD2 + let required_size2
			
			current_next_chunk first_free_chunk -    required_size1 = if
				first_free_chunk let chunk1
				
				chunk1    current_next_chunk chunk1 -     set_chunk_size
				chunk1    type                 set_chunk_state
				chunk1 CHUNK_OVERHEAD + ret
				
			else current_next_chunk first_free_chunk - required_size2 >= elif
				first_free_chunk let chunk1
				first_free_chunk bytes CHUNK_OVERHEAD + + let chunk2
				
				chunk1    chunk2 chunk1 -     set_chunk_size
				chunk1    type     set_chunk_state
				chunk2    current_next_chunk chunk2 -  set_chunk_size
				chunk2    CHUNK_FREE          set_chunk_state
				chunk1 CHUNK_OVERHEAD + ret
				
			end
		end
		
		current_next_chunk let current_chunk
	end
end

func free ptr do
	ptr CHUNK_OVERHEAD - CHUNK_FREE set_chunk_state
end

func memsize ptr do
	ptr CHUNK_OVERHEAD - ?ptr CHUNK_OVERHEAD -
end

#
# private functions
#

func setup_mem do
	#TODO 
	"A" let ptr
	ptr ?8 0 = if ret end
	ptr 0 !8

	get_start_chunk get_end_chunk get_start_chunk - set_chunk_size
	get_start_chunk CHUNK_FREE set_chunk_state
end

func get_start_chunk do mem_start end
func get_end_chunk do mem_start 256 256 256 * * + end
func get_next_chunk chunk do chunk chunk get_chunk_size + end

func get_chunk_size chunk do chunk ?ptr end
func set_chunk_size chunk size do chunk size !ptr end

func get_chunk_state chunk do chunk sizeof(ptr) + ?ptr end
func set_chunk_state chunk state do chunk sizeof(ptr) + state !ptr end

func ptr_to_chunk ptr do ptr sizeof(ptr) 2 * - end

func CHUNK_OVERHEAD do sizeof(ptr) 2 * end
func CHUNK_OVERHEAD2 do CHUNK_OVERHEAD 2 * end

func CHUNK_FREE do 1 end
func CHUNK_ALLOCED do 2 end
