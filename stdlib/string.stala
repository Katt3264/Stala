# string manipulation

func strcpy dst src do
	while true if
		src ?8 let ch
		dst ch !8
		ch 0 = if ret end
		src ++ let src
		dst ++ let dst
	end
end

func strcat dst src do
	dst dst strlen + let dst
	dst src strcpy
end

func strcatchr dst char do
	dst dst strlen + let dst
	dst char !8
	dst ++ 0 !8
end

# string examination

func strlen str do
	0 let i
	while str i + ?8 0 != if
		i 1 + let i
	end
	i ret
end

func strchr str chr do
	while true if
		str ?8 let c
		c chr = if str ret end
		c 0 = if null ret end
		str ++ let str
	end
end

func strstr str substr do
	substr strlen let len
	while true if
		str ?8 0 = if null ret end
		str substr len strncmp 0 = if str ret end
		str ++ let str
	end
end

func streq str1 str2 do
	str1 str2 strcmp 0 =
end

# str str - int
func strcmp str1 str2 do
	while true if
		str1 ?8 let ch1
		str2 ?8 let ch2
		
		ch1 ch2 < if
			-1 ret
		else ch1 ch2 > elif
			1 ret
		else 
			ch1 0 = if 0 ret end
		end
		str1 ++ let str1
		str2 ++ let str2
	end
end

func strncmp str1 str2 len do
	while true if
		str1 ?8 let ch1
		str2 ?8 let ch2
		
		len 0 = if 0 ret end
		
		ch1 ch2 < if
			-1 ret
		else ch1 ch2 > elif
			1 ret
		else 
			#equality - both end reached
			ch1 0 = if 0 ret end
		end
		str1 ++ let str1
		str2 ++ let str2
		len -- let len
	end
end

# memory functions

func memcpy src dst len do
	while len 0 != len -- let len if
		src ?8 let char
		dst char !8
		src ++ let src
		dst ++ let dst
	end
end

# convertion

func is_str_int str base do
	str base try_str_to_int let val let status
	status
end


func str_to_int str base do
	str base try_str_to_int let val let status
	status false = if "string could not be converted to int: " fail str fail exit end
	val
end


func try_str_to_int str base do
	base 2 < base 16 > OR if "invalid base" fail exit end
	1 let factor
	str ?8 "-" ?8 = if
		-1 let factor str ++ let str 
	else str ?8 "+" ?8 = elif
		#positive number
	end
	
	0 let value
	
	str ?8 0 = if false 0 ret end
	
	
	while str ?8 0 != if
	
		str ?8 let char
		
		value base * let value
		char "0" ?8 = if
			value 0 + let value
		else char "1" ?8 = elif
			value 1 + let value
		else char "2" ?8 = base 2 > AND elif
			value 2 + let value
		else char "3" ?8 = base 3 > AND elif
			value 3 + let value
		else char "4" ?8 = base 4 > AND elif
			value 4 + let value
		else char "5" ?8 = base 5 > AND elif
			value 5 + let value
		else char "6" ?8 = base 6 > AND elif
			value 6 + let value
		else char "7" ?8 = base 7 > AND elif
			value 7 + let value
		else char "8" ?8 = base 8 > AND elif
			value 8 + let value
		else char "9" ?8 = base 9 > AND elif
			value 9 + let value
		else char "a" ?8 = base 10 > AND elif
			value 10 + let value
		else char "b" ?8 = base 11 > AND elif
			value 11 + let value
		else char "c" ?8 = base 12 > AND elif
			value 12 + let value
		else char "d" ?8 = base 13 > AND elif
			value 13 + let value
		else char "e" ?8 = base 14 > AND elif
			value 14 + let value
		else char "f" ?8 = base 15 > AND elif
			value 15 + let value
		else
			false 0 ret
		end
		str ++ let str
	end
	
	true value factor *
end

func int_to_str dst val base do
	
	base 2 < base 16 > OR if "invalid base" fail exit end
	
	val 0 < if
		dst "-" ?8 !8
		dst ++ let dst
		val -1 * let val
	end
	
	val 0 = if
		dst "0" ?8 !8
		dst ++ let dst
	end
	
	dst let ptr
	"0123456789abcdef" let num_to_char
	
	while val 0 != if
		val base % let remainder
		val base / let val
		ptr num_to_char remainder + ?8 !8
		
		ptr ++ let ptr
	end
	
	ptr 0 !8
	ptr -- let ptr
	
	while ptr dst > if
		ptr ?8 let v1
		dst ?8 let v2
		
		ptr v2 !8
		dst v1 !8
		
		ptr -- let ptr
		dst ++ let dst
	end
	
end






